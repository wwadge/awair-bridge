plugins {
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.google.cloud.tools.jib' version '3.2.1'
    id 'org.springframework.experimental.aot' version '0.11.3'
}

jib{
    to.image = 'docker.io/wwadge/awair-bridge:latest'
    from{
        image = 'eclipse-temurin:17'
        platforms {
            platform {
                architecture = 'amd64'
                os = 'linux'
            }
            platform {
                architecture = 'arm64'
                os = 'linux'
            }


        }
    }
}

task jibArm {
    doFirst {
        jib.to.image = "wwadge/awair-bridge:armv7"
        jib.from {
            image = "arm32v7/eclipse-temurin:latest"
            platforms {
                platform {
                    architecture = 'arm'
                    os = 'linux'
                }
            }
        }
    }
    finalizedBy 'jib'
}
ext {
    set('springCloudVersion', "2021.0.1")
}


group = 'com.pw'
version = '0.0.2'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    maven { url 'https://repo.spring.io/release' }
    mavenCentral()
}


dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

ext {
    set('keycloakVersion', '4.8.3.Final')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j:2.1.0'

    implementation 'org.jsoup:jsoup:1.14.3'
    implementation 'com.google.firebase:firebase-admin:8.1.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'


}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootBuildImage') {
    builder = 'paketobuildpacks/builder:tiny'
    environment = ['BP_NATIVE_IMAGE': 'true']
}

